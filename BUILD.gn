# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("gdbserver") {
  testonly = true

  deps = [
    ":bin",
    ":gdbserver_unittests",
    "test_apps",
  ]
}

executable("bin") {
  output_name = "debugserver"

  sources = [
    "command_handler.cc",
    "command_handler.h",
    "exception_port.cc",
    "exception_port.h",
    "registers.cc",
    "registers.h",
    "io_loop.cc",
    "io_loop.h",
    "main.cc",
    "process.cc",
    "process.h",
    "server.cc",
    "server.h",
    "thread.cc",
    "thread.h",
    "util.cc",
    "util.h",
  ]

  if (current_cpu == "x64") {
    sources += [ "registers_amd64.cc" ]
  } else if (current_cpu == "arm64") {
    sources += [ "registers_arm64.cc" ]
  } else {
    sources += [ "registers_default.cc" ]
  }

  deps = [
    "//lib/ftl",
    "//lib/mtl",
    "//magenta/system/ulib/mx",
  ]

  libs = [
    "launchpad",
    "magenta",
  ]
}

executable("gdbserver_unittests") {
  testonly = true

  sources = [
    "test/run_all_unittests.cc",
    "util.cc",
    "util.h",
    "util_unittest.cc",
  ]

  deps = [
    "//lib/ftl",
    "//third_party/gtest",
  ]

  libs = [
    "magenta",
  ]
}
